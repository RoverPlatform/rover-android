import org.gradle.api.internal.artifacts.dependencies.DefaultExternalModuleDependency

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'maven-publish'
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode rover_sdk_version_code
        versionName rover_sdk_version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        // debug {
        //     minifyEnabled true
        // }
    }

    // Add Kotlin source directory to all source sets
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
    }
}

androidExtensions {
    // So we can use @Parcelize.  This does not expose risk to consumers; this is a build time-only
    // consideration.
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // TODO: should these use 'api' instead of compile? Unclear on guidance on transitive AndroidX
    // dependencies within libraries.
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:transition:27.1.1'
    // Until java.util.concurrent.Flow appears in Android SDK, import:
    api 'org.reactivestreams:reactive-streams:1.0.2'
    api 'android.arch.lifecycle:extensions:1.1.1'
    api "android.arch.work:work-runtime-ktx:1.0.0-alpha10"
    implementation 'com.android.support:customtabs:27.1.1'
    implementation 'com.android.support:transition:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    implementation 'com.android.support:customtabs:27.1.1'

    // Shadow android.jar's stubbed version of JSON in tests:
    testImplementation 'org.json:json:20180130'

    // spek
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'

    // JUnit 5 support.  Needed for Spek.
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.3.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.3.2")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.27.0"
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task webDocsDokka (type: org.jetbrains.dokka.gradle.DokkaTask, overwrite: true) {
    outputFormat = 'jekyll'
    outputDirectory = "$buildDir/jekylldoc"

    // Suppress a package
    packageOptions {
        prefix = "io.rover.shaded"
        suppress = true
    }

    // Suppress a package
    packageOptions {
        prefix = "android"
        suppress = true
    }
}

ext {
    groupId = 'io.rover' // package name of the project
    artifactId = "sdk" // module name of the library
    libVersion = rover_sdk_version
}

//just copy paste the lines below
if (project.hasProperty("kotlin")) { //Kotlin libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc, dependsOn: dokka) {

    }
} else if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    // options.encoding = 'UTF-8'
}

publishing {
    repositories {
        maven {
            url System.getenv('DEPLOY_MAVEN_PATH')
        }
    }

    publications {
        production(MavenPublication) {
            artifact("$buildDir/outputs/aar/$artifactId-release.aar")
            groupId "io.rover"

            version rover_sdk_version

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each

                def apiAndImplementationDeps = configurations.implementation.allDependencies + configurations.compile.allDependencies

                // get all the api *and* implementation dependencies
                apiAndImplementationDeps.each {
                    if(it instanceof DefaultExternalModuleDependency) {
                        println("ITERATING OVER DEPENDENCY: $it")
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', "compile")
                    }
                }

                // notice that project scope dependencies are not handled. Instead, we must add them
                // as external dependencies manually.

            }

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}
